plugins {
	id 'groovy-gradle-plugin'
}

Properties projectProperties = new Properties()
try (FileInputStream fis = new FileInputStream("$rootDir/../gradle.properties")) {
	projectProperties.load(fis)
}
for (Map.Entry<String,String> entry : projectProperties.entrySet()) {
	if (!project.hasProperty(entry.getKey())) {
		ext[entry.getKey()] = entry.getValue()
	}
}

repositories {
	if (rootProject.hasProperty('repoUrlPrefix')) {
		maven {
			url "${repoUrlPrefix}/maven-public/"
			allowInsecureProtocol true
		}
		maven {
			url "${repoUrlPrefix}/gradle-plugins/"
			allowInsecureProtocol true
		}
	} else {
		gradlePluginPortal()
		mavenCentral()
	}
}

dependencies {
	implementation "io.freefair.lombok:io.freefair.lombok.gradle.plugin:${property('lombok-plugin.version')}"
	implementation "io.spring.gradle:dependency-management-plugin:${property('dependency-management-plugin.version')}"
	implementation "io.spring.javaformat:spring-javaformat-gradle-plugin:${property('javaformat-plugin.version')}"
	implementation "org.springframework.boot:org.springframework.boot.gradle.plugin:${property('spring-boot.version')}"
}