plugins {
	id 'groovy-gradle-plugin'
}

Properties projectProperties = new Properties()
try (FileInputStream fis = new FileInputStream("$rootDir/../gradle.properties")) {
	projectProperties.load(fis)
}
for (Map.Entry<String,String> entry : projectProperties.entrySet()) {
	if (!project.hasProperty(entry.getKey())) {
		ext[entry.getKey()] = entry.getValue()
	}
}

repositories {
	if (rootProject.hasProperty('repoUrlPrefix')) {
		maven {
			url "${repoUrlPrefix}/gradle-plugins/"
			allowInsecureProtocol true
		}
	} else {
		gradlePluginPortal()
	}
}

dependencies {
	implementation "io.freefair.lombok:io.freefair.lombok.gradle.plugin:${project.properties['lombok-plugin.version']?:'latest.release'}"
	implementation "io.spring.dependency-management:io.spring.dependency-management.gradle.plugin:${project.properties['dependency-management-plugin.version']?:'latest.release'}"
	implementation "io.spring.javaformat:io.spring.javaformat.gradle.plugin:${project.properties['javaformat-plugin.version']?:'latest.release'}"
	implementation "org.springframework.boot:org.springframework.boot.gradle.plugin:${project.properties['spring-boot.version']?:'latest.release'}"
}