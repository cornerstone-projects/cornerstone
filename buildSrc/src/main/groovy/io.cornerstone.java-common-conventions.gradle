plugins {
	id 'java'
	id 'io.freefair.lombok'
	id 'io.spring.dependency-management'
}

sourceCompatibility = '11'

repositories {
	if (rootProject.hasProperty('repoUrlPrefix')) {
		maven {
			url "${repoUrlPrefix}/maven-public/"
			allowInsecureProtocol true
		}
	} else {
		mavenCentral()
	}
}

dependencyManagement {
	imports {
		mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
	}
	generatedPomCustomization {
		enabled = false
	}
}

sourceSets {
	integrationTest {
		compileClasspath += sourceSets.main.output
		compileClasspath += sourceSets.test.output
		runtimeClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.test.output
	}
}

configurations {
	integrationTestImplementation.extendsFrom implementation
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom runtimeOnly
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testImplementation "org.springframework.security:spring-security-test"
	testRuntimeOnly "com.h2database:h2"
	testImplementation(testFixtures(project(":cornerstone-core")))
	integrationTestImplementation(testFixtures(project(":cornerstone-core")))
}

configurations.all {
	resolutionStrategy.eachDependency {
		if (it.requested.name == 'spring-boot-starter-logging')
			it.useTarget "${it.requested.group}:spring-boot-starter-log4j2:${it.requested.version}"
	}
}

tasks.withType(Test) {
	useJUnitPlatform()
}

java {
	withSourcesJar()
}

jar {
	manifest {
		 attributes(
		 	'Implementation-Title': project.name,
			'Implementation-Version': project.version,
			'Automatic-Module-Name': project.name.replace('-', '.')  // for Jigsaw
		)
	}
}

sourcesJar {
	dependsOn delombok
	from 'build/generated/sources/delombok/java/main/'
	duplicatesStrategy DuplicatesStrategy.INCLUDE
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	options.compilerArgs << '-parameters'
}

clean {
	doLast {
		project.delete 'bin', 'logs'
	}
}

tasks.register('integrationTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	shouldRunAfter test
}

if (rootProject.hasProperty('integration')) {
	check.dependsOn integrationTest
}
